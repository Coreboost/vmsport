Form MRF_FORM

  Form Data
    EMPLOYEE_NAME Character(32)
    BADGE_NUMBER Integer(6)
    COST_CENTER Character(3)
    ADDRESS Character(120)
    REASON Character(64)
    N Unsigned Longword
    BLANK_DATE Datetime(8)
    Group TRIP
      Occurs 36
      Current N
      TRIP_DATE Datetime(8)
      TRIP_FROM Character(3)
      TRIP_TO Character(3)
      MILES Longword Integer
      AMOUNT Longword Integer
      TOLL Longword Integer
      SUBTOTAL Longword Integer
    End Group
    TOTAL_MILES Longword Integer
    TOTAL_AMOUNT Longword Integer
    TOTAL_TOLLS Longword Integer
    FORM_TOTAL Longword Integer
  End Data

  Form Record HEADER_INFO_RECORD
    EMPLOYEE_NAME Character(32)
    BADGE_NUMBER Character(6)
    COST_CENTER Character(3)
    ADDRESS Character(120)
    REASON Character(64)
  End Record

  Form Record TRIP_ROW_RECORD
    Group TRIP
      TRIP_DATE Datetime(8)
      Using TRIP(N).TRIP_DATE
      TRIP_FROM Character(3)
      Using TRIP(N).TRIP_FROM
      TRIP_TO Character(3)
      Using TRIP(N).TRIP_TO
      MILES Longword Integer
      Using TRIP(N).MILES
      AMOUNT Longword Integer
      Using TRIP(N).AMOUNT
      TOLL Longword Integer
      Using TRIP(N).TOLL
      SUBTOTAL Longword Integer
      Using TRIP(N).SUBTOTAL
    End Group
  End Record

  Form Record TRIPS_RECORD
    Group TRIP
      Occurs 36
      TRIP_DATE Datetime(8)
      TRIP_FROM Character(3)
      TRIP_TO Character(3)
      MILES Longword Integer
      AMOUNT Longword Integer
      TOLL Longword Integer
      SUBTOTAL Longword Integer
    End Group
  End Record

  Form Record TOTALS_RECORD
    TOTAL_MILES Longword Integer
    TOTAL_AMOUNT Longword Integer
    TOTAL_TOLLS Longword Integer
    FORM_TOTAL Longword Integer
  End Record

  Layout VT_LAYOUT
    Device
      Terminal
      Type %VT100
    End Device

    Size 24 Lines by 80 Columns

    List LOCATION_CODES
      "ACT"
      "BXB"
      "LTN"
      "MKO"
      "ZKO"
    End List

    Viewport MAIN_VIEWPORT
      Lines 1 Through 22
      Columns 1 Through 80
    End Viewport

    Viewport DEFAULT_MESSAGE_VIEWPORT
      Lines 23 Through 24
      Columns 1 Through 80
    End Viewport

    Viewport HELP_VIEWPORT
      Lines 1 Through 22
      Columns 1 Through 80
    End Viewport

    Function DO_TOTALS
      Is %DO
      (%PF1 %KP_PERIOD)
    End Function

    Function DISCARD
      Is %F8
      (%PF1 %KP_8)
    End Function

    Function INSERT LINE
      Is %CARRIAGE_RETURN
    End Function

    Function EXIT GROUP NEXT
      Is (%PF4 %HORIZONTAL_TAB)
        (%PF4 %CARRIAGE_RETURN)
        (%PF4 %ENTER)
        (%PF1 %DOWN)
    End Function

    Function MOVE_UP
      Is %UP
    End Function

    Function MOVE_DOWN
      Is %DOWN
    End Function

    Function SELECT
      Is %SELECT
      %KP_PERIOD
    End Function

    Function PRINT_FILE
      Is %CONTROL_P
    End Function

    Internal Response BORDER_PATROL
      Message
        "No items in that direction."
      Signal
    End Response

    Internal Response RESET_DATA
      Reset All
      Message
        "Data discarded."
      Signal
      Position To Panel TRIPS_INFO_PANEL
    End Response

    Internal Response RECORD_DATA
      Message
        "Data received. Program exiting."
      Signal
      Return Immediate
    End Response

    Internal Response UPDATE_TOTALS
      Message
        "Computing totals..."
      Call "DO_TOTALS" Using
        By Reference TRIPS_RECORD
        By Reference TOTAL_MILES
        By Reference TOTAL_AMOUNT
        By Reference TOTAL_TOLLS
        By Reference FORM_TOTAL
      Message
        "Done computing totals."
      Signal
    End Response

    Disable Response
      Remove All
    End Response

    Receive Response HEADER_INFO_RECORD
      Activate
      Panel HEADER_INFO_PANEL
      Position To Panel HEADER_INFO_PANEL
    End Response

    Receive Response TRIPS_RECORD
      Activate
      Panel TRIPS_INFO_PANEL
      Position To Panel TRIPS_INFO_PANEL
    End Response

    Function Response DISCARD
      Include RESET_DATA
    End Response

    Function Response MOVE_UP
      If ( NOT UPPERMOST ITEM) Then
        Position To Up Item
      Else
        Include BORDER_PATROL
      End If
    End Response

    Function Response MOVE_DOWN
      If ( NOT LOWERMOST ITEM) Then
        Position To Down Item
      Else
        Include BORDER_PATROL
      End If
    End Response

    Function Response PRINT_FILE
      Message
        "Preparing the print file..."
      Print
        TRIPS_INFO_PANEL
      Message
        "Print file completed."
    End Response

    Use Help Panel HELP_PANEL

    Field Default BOLD_ICON
      Active Highlight Bold
    End Default

    Apply Field Default Of
      Active Highlight Underlined
    End Default

    Message Panel DEFAULT_MESSAGE_PANEL
      Viewport DEFAULT_MESSAGE_VIEWPORT
    End Panel

    Help Panel HELP_PANEL
      Viewport HELP_VIEWPORT
      Function Response NEXT HELP
        Activate Panel SECOND_HELP_PANEL
        Position To Panel SECOND_HELP_PANEL
      End Response

      Literal Text
        Line 1
        Column 18
        Value "HELP FOR THE MILEAGE REIMBURSEMENT APPLICATION"
        Display
        Bold
      End Literal

      Literal Polyline
        Line 2 Column 1
        Line 2 Column 80
        Display
        Bold
      End Literal

      Literal Rectangle
        Line 4 Column 1
        Line 18 Column 80
      End Literal

      Literal Polyline
        Line 4 Column 40
        Line 18 Column 40
      End Literal

      Literal Text
        Line 3
        Column 14
        Value "To:"
        Display
        Bold
      End Literal

      Literal Text
        Line 3
        Column 59
        Value "VT"
        Display
        Bold
      End Literal

      Literal Text
        Line 5
        Column 3
        Value "Move to next item"
      End Literal

      Literal Text
        Line 5
        Column 42
        Value "Tab or KP Enter"
      End Literal

      Literal Text
        Line 6
        Column 3
        Value "Move to previous item"
      End Literal

      Literal Text
        Line 6
        Column 42
        Value "F12 or Backspace"
      End Literal

      Literal Text
        Line 7
        Column 3
        Value "Display help message"
      End Literal

      Literal Text
        Line 7
        Column 42
        Value "PF2 or Help"
      End Literal

      Literal Text
        Line 8
        Column 3
        Value "Leave help"
      End Literal

      Literal Text
        Line 8
        Column 42
        Value "PF1-PF2 or PF1-Help"
      End Literal

      Literal Text
        Line 9
        Column 6
        Value "Header Screen:"
        Display
        Bold
      End Literal

      Literal Text
        Line 10
        Column 3
        Value "Next line of address"
      End Literal

      Literal Text
        Line 10
        Column 42
        Value "Return"
      End Literal

      Literal Text
        Line 11
        Column 3
        Value "Move to next screen"
      End Literal

      Literal Text
        Line 11
        Column 42
        Value "F10 or Ctrl/Z"
      End Literal

      Literal Text
        Line 12
        Column 6
        Value "Trips Screen:"
        Display
        Bold
      End Literal

      Literal Text
        Line 13
        Column 3
        Value "Move to option buttons"
      End Literal

      Literal Text
        Line 13
        Column 42
        Value "PF1-down arrow"
      End Literal

      Literal Text
        Line 14
        Column 3
        Value "Choose an option"
      End Literal

      Literal Text
        Line 14
        Column 42
        Value "Select or KP."
      End Literal

      Literal Text
        Line 15
        Column 3
        Value "Compute totals"
      End Literal

      Literal Text
        Line 15
        Column 42
        Value "Do or PF1-KP."
      End Literal

      Literal Text
        Line 16
        Column 3
        Value "Discard data"
      End Literal

      Literal Text
        Line 16
        Column 42
        Value "F8 or PF1-KP8"
      End Literal

      Literal Text
        Line 17
        Column 3
        Value "Record and exit"
      End Literal

      Literal Text
        Line 17
        Column 42
        Value "F10 or Ctrl/Z"
      End Literal

      Icon MORE_HELP
        Apply Field Default BOLD_ICON

        Function Response SELECT
          Activate
          Panel SECOND_HELP_PANEL
          Position To Panel SECOND_HELP_PANEL
        End Response

        Literal Rectangle
          Line 19 Column 16
          Line 21 Column 33
        End Literal

        Literal Text
          Line 20
          Column 17
          Value "Help on Printing"
        End Literal
      End Icon

      Icon LEAVE_HELP
        Apply Field Default BOLD_ICON

        Function Response SELECT
          Exit Help
          Remove Help
        End Response

        Literal Text
          Line 20
          Column 44
          Value "Dismiss Help"
        End Literal

        Literal Rectangle
          Line 19 Column 43
          Line 21 Column 56
        End Literal
      End Icon
    End Panel

    Panel HEADER_INFO_PANEL
      Viewport MAIN_VIEWPORT
      Display  %Keypad_Application

      Entry Response
        Message
          "Press Help for information on how to use this application."
      End Response

      Exit Response
        Message
          "Displaying next screen..."
      End Response

      Literal Text
        Line 1
        Column 15
        Value "Mileage Reimbursement Form"
        Display
          Font Size Double Wide
      End Literal

      Literal Text
        Line 2
        Column 23
        Value "Header Information"
        Display
          Font Size Double Wide
      End Literal

      Literal Polyline
        Line 3 Column 1
        Line 3 Column 80
      End Literal

      Literal Text
        Line 5
        Column 2
        Value "Employee Name: "
      End Literal

      Field EMPLOYEE_NAME
        Line 5
        Column 17
      End Field

      Literal Text
        Line 7
        Column 3
        Value "Badge Number:"
      End Literal

      Field BADGE_NUMBER
        Line 7
        Column 17
        Output Picture 999999R
        Justification Right
      End Field

      Literal Text
        Line 7
        Column 30
        Value "Cost Center:"
      End Literal

      Field COST_CENTER
        Line 7
        Column 43
        Autoskip
        Uppercase
      End Field

      Literal Text
        Line 9
        Column 8
        Value "Address:"
      End Literal

      Textfield ADDRESS
        Line 9
        Column 17
        Builtin Function Response CURSOR UP
        Builtin Function Response CURSOR DOWN
        Rows 3
        Columns 40
      End Field

      Literal Text
        Line 13
        Column 2
        Value "Reason for"
      End Literal

      Literal Text
        Line 14
        Column 2
        Value "Reimbursement:"
      End Literal

      Field REASON
        Line 14
        Column 17
      End Field

      Literal Text
        Line 17
        Column 17
        Value "To move to the previous item:"
      End Literal

      Literal Text
        Line 16
        Column 17
        Value "To move to the next item:"
      End Literal

      Literal Text
        Line 19
        Column 17
        Value "When you are finished:"
      End Literal

      Literal Text
        Line 20
        Column 17
        Value "For more information:"
      End Literal

      Literal Text
        Line 16
        Column 50
        Value "Tab or Enter"
        Display
        Bold
      End Literal

      Literal Text
        Line 17
        Column 50
        Value "F12 or Backspace"
        Display
        Bold
      End Literal

      Literal Text
        Line 19
        Column 50
        Value "F10 or Ctrl/Z"
        Display
        Bold
      End Literal

      Literal Text
          Line 20
          Column 50
          Value "PF2 or Help"
          Display
          Bold
      End Literal

      Literal Rectangle
        Line 15 Column 16
        Line 21 Column 68
      End Literal

      Literal Text
        Line 18
        Column 17
        Value "To move to next line of address:"
      End Literal

      Literal Text
        Line 18
        Column 50
        Value "Return"
        Display
        Bold
      End Literal
    End Panel

    Panel TRIPS_INFO_PANEL
      Viewport MAIN_VIEWPORT
      Display
        %Keypad_Application

      Entry Response
        Message
          "Press Help for information on how to use this application."
      End Response

      Function Response TRANSMIT
        Include UPDATE_TOTALS
        Include RECORD_DATA
      End Response

      Function Response DO_TOTALS
        Include UPDATE_TOTALS
      End Response

      Literal Text
        Line 1
        Column 15
        Value "Mileage Reimbursement Form"
        Display
        Font Size Double Wide
      End Literal

      Literal Polyline
        Line 2 Column 1
        Line 2 Column 80
      End Literal

      Literal Text
        Line 3
        Column 11
        Value "Date"
      End Literal

      Literal Text
        Line 4
        Column 8
        Value "DD-MMM-YYYY"
      End Literal

      Literal Rectangle
        Line 5 Column 6
        Line 18 Column 69
      End Literal

      Literal Text
        Line 3
        Column 21
        Value "Travel Points"
      End Literal

      Literal Text
        Line 3
        Column 39
        Value "Personal Car"
      End Literal

      Literal Text
        Line 4
        Column 22
        Value "From"
      End Literal

      Literal Text
        Line 4
        Column 33
        Value "To"
      End Literal

      Literal Text
        Line 4
        Column 39
        Value "Miles"
      End Literal

      Literal Text
        Line 4
        Column 46
        Value "Amount"
      End Literal

      Literal Text
        Line 4
        Column 53
        Value "Tolls"
      End Literal

      Literal Text
        Line 4
        Column 61
        Value "Subtotal"
      End Literal

      Group TRIP
        Vertical
        Displays 12
        Function Response MOVE_UP
          If ( NOT FIRST OCCURRENCE VERTICAL) Then
            Position To Up Occurrence
          Else
            Include BORDER_PATROL
          End If
        End Response

        Function Response MOVE_DOWN
          If ( NOT LAST OCCURRENCE VERTICAL) Then
            Position To Down Occurrence
          Else
            Include BORDER_PATROL
          End If
        End Response

        Field TRIP_DATE
          Line 6
          Column 8
          Autoskip
          Input Picture For Date DD-AAA-YYYY
          Output " " When (TRIP(**).TRIP_DATE = BLANK_DATE)
        End Field

        Field TRIP_FROM
          Line 6
          Column 22
          Autoskip
          Uppercase
          Protected When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Search LOCATION_CODES
          Message
            "Must be a valid code: ACT, BXB, LTN, MKO, or ZKO"
        End Field

        Field TRIP_TO
          Line 6
          Column 29
          Autoskip
          Uppercase
          Protected When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Search LOCATION_CODES
          Message
            "Must be a valid code: ACT, BXB, LTN, MKO, or ZKO"
        End Field

        Field MILES
          Line 6
          Column 40
          Exit Response
            Call "UPDATE_ROW" Using
            By Reference TRIP_ROW_RECORD
          End Response
          Output Picture 999R
          Justification Right
          Output "" When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Protected When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Range 1 Through 100
          Message
          "You cannot get reimbursed for more than 100 miles per trip!"
        End Field

        Field AMOUNT
          Line 6
          Column 46
          Output Picture 99R9.99
          Scale -2
          Output ""
          When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Protected
        End Field

        Field TOLL
          Line 6
          Column 54
          Exit Response
            Let TRIP(N).SUBTOTAL = (TRIP(N).AMOUNT + TRIP(N).TOLL)
          End Response
          Output Picture 99R9.99
          Scale -2
          Justification Decimal
          Output ""
          When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Protected
          When (TRIP(**).TRIP_DATE = BLANK_DATE)
        End Field

        Field SUBTOTAL
          Line 6
          Column 62
          Output Picture 99R9.99
          Scale -2
          Output ""
          When (TRIP(**).TRIP_DATE = BLANK_DATE)
          Protected
        End Field
      End Group

      Field TOTAL_MILES
        Line 19
        Column 39
        Output Picture 999R9
        Protected
      End Field

      Field TOTAL_AMOUNT
        Line 19
        Column 45
        Output Picture 999R9.99
        Scale -2
        Protected
      End Field

      Field TOTAL_TOLLS
        Line 19
        Column 54
        Output Picture 999R.99
        Scale -2
        Protected
      End Field

      Literal Text
        Line 19
        Column 31
        Value "Totals:"
      End Literal

      Field FORM_TOTAL
        Line 19
        Column 61
        Output Picture 999R9.99
        Scale -2
        Protected
      End Field

      Icon COMPUTE_TOTALS
        Apply Field Default BOLD_ICON
        Function Response SELECT
          Include UPDATE_TOTALS
        End Response

        Literal Rectangle
          Line 20 Column 2
          Line 22 Column 19
        End Literal

        Literal Text
          Line 21
          Column 4
          Value "Compute Totals"
        End Literal
      End Icon

      Icon DISCARD_DATA
        Apply Field Default BOLD_ICON
        Function Response SELECT
          Include RESET_DATA
        End Response
        Literal Rectangle
          Line 20 Column 22
          Line 22 Column 37
        End Literal
        Literal Text
          Line 21
          Column 24
          Value "Discard Data"
        End Literal
      End Icon

      Icon RECORD_AND_EXIT
        Apply Field Default BOLD_ICON
        Function Response SELECT
          Include UPDATE_TOTALS
          Include RECORD_DATA
        End Response
        Literal Rectangle
          Line 20 Column 40
          Line 22 Column 58
        End Literal
        Literal Text
          Line 21
          Column 42
          Value "Record and Exit"
        End Literal
      End Icon

      Icon HELP_ICON
        Apply Field Default BOLD_ICON
        Function Response SELECT
          Enter Help
        End Response
        Literal Rectangle
          Line 20 Column 61
          Line 22 Column 75
        End Literal
        Literal Text
          Line 21
          Column 66
          Value "Help"
        End Literal
      End Icon
    End Panel

    Help Panel SECOND_HELP_PANEL
      Viewport HELP_VIEWPORT

      Literal Text
        Line 1
        Column 24
        Value "HELP ON PRINTING FORM DATA"
        Display
        Bold
      End Literal

      Literal Polyline
        Line 2 Column 1
        Line 2 Column 80
      End Literal

      Literal Text
        Line 3
        Column 1
        Value "When you finish entering the data for your form, you can press Ctrl/P to"
      End Literal

      Literal Text
        Line 4
        Column 1
        Value "create a file that contains a picture of the second screen:"
      End Literal

      Literal Text
        Line 5
        Column 7
        Value "Platform"
        Display
        Underlined
      End Literal

      Literal Text
        Line 5
        Column 30
        Value "Files Created"
        Display
        Underlined
      End Literal

      Literal Text
        Line 5
        Column 56
        Value "Description"
        Display
        Underlined
      End Literal

      Literal Text
        Line 6
        Column 10
        Value "VT"
      End Literal

      Literal Text
        Line 6
        Column 30
        Value "mrf_form.txt"
      End Literal

      Literal Text
        Line 6
        Column 56
        Value "Text file "
      End Literal

      Literal Polyline
        Line 9 Column 1
        Line 9 Column 80
      End Literal

      Literal Text
        Line 10
        Column 1
        Value "On all platforms, all your form data is formatted for quality printing."
      End Literal

      Literal Text
        Line 11
        Column 1
        Value "On OpenVMS, the application creates a file named prin_mrf.doc."
      End Literal

      Literal Polyline
        Line 12 Column 1
        Line 12 Column 80
      End Literal

      Literal Text
        Line 14
        Column 1
        Value "On OpenVMS, to convert a .doc file to PostScript format and print the file,"
      End Literal

      Literal Text
        Line 15
        Column 1
        Value "use the following commands:"
      End Literal

      Literal Text
        Line 16
        Column 1
        Value "$ CONVERT/DOCUMENT filename.DOC filename.PS/FORMAT=PS"
      End Literal

      Literal Text
        Line 17
        Column 1
        Value "$ PRINT /QUEUE=postscript_queue_name file_name.PS"
      End Literal

      Icon PREV_HELP
        Apply Field Default BOLD_ICON
          Function Response SELECT
            Activate
            Panel HELP_PANEL
            Position To Panel HELP_PANEL
          End Response

          Literal Rectangle
            Line 19 Column 15
            Line 21 Column 29
          End Literal

          Literal Text
            Line 20
            Column 16
            Value "Previous Help"
          End Literal
        End Icon

        Icon LEAVE_SECOND_HELP
        Apply Field Default BOLD_ICON
        Function Response SELECT
          Exit Help
          Remove Help
        End Response

        Literal Rectangle
          Line 19 Column 42
          Line 21 Column 55
        End Literal

        Literal Text
          Line 20
          Column 43
          Value "Dismiss Help"
        End Literal
      End Icon
    End Panel
  End Layout
End Form
